#!/usr/bin/perl -w

use strict;

use FindBin qw/$Bin/;
use lib "$Bin/../../lib";

use Wikia::LB;

my $wikis = wikis_to_clean();
die "No wikis found\n" unless $wikis;

print STDERR "== Removing bad video links from imagelinks table ==\n";

my $script_start = time;

my $total = scalar(@$wikis);
my $idx = 1;
foreach my $dbname (@$wikis) {
	cleanup_wiki($dbname, $idx++, $total);
}

my $delta = time - $script_start;
print STDERR "Finished in $delta s\n";

sub wikis_to_clean {
	my $dbh = Wikia::LB->instance->getConnection(Wikia::LB::DB_SLAVE, undef, 'dataware', undef, 1);
	my $sql = "SELECT DISTINCT wiki_dbname
			   FROM video_migration_log
			   WHERE action_name='POSTMIGRATION'
			     AND action_status='STOP'";

	my $ret = $dbh->selectall_arrayref($sql);

	return unless $ret and @$ret;
	return [map { $_->[0] } @$ret];
}

sub cleanup_wiki {
	my ($dbname, $idx, $total) = @_;
	my $dbh = Wikia::LB->instance->getConnection(Wikia::LB::DB_MASTER, undef, $dbname, undef, 1);
	return unless $dbh;

	my $delete_start = time;

	print STDERR "-- [$idx/$total] Cleaning $dbname ... ";
	my $rows = $dbh->do("DELETE FROM imagelinks WHERE il_to like ':%'");
	$dbh->disconnect;

	my $delta = time-$delete_start;
	print STDERR " done (removed $rows rows in $delta s)\n";
}
