#!/usr/bin/perl -w

use strict;

use FindBin qw/$Bin/;
use lib "$Bin/../../lib";

use Getopt::Long;
use IPC::Open3;
use DateTime::Format::Strptime;

use Wikia::DW::Common;
use Wikia::DW::ETL::Base;

my ($table, $periods, $begin, $end, $rebuild, $debug) = undef;

# Defaults
$rebuild = 0;

GetOptions( 'table=s'   => \$table,
            'periods:s' => \$periods,
            'begin:s'   => \$begin,
            'end:s'     => \$end,
            'rebuild!'  => \$rebuild,
            'debug+'    => \$debug );

Wikia::DW::Common::log('Running etl...');
Wikia::DW::Common::exit_if_running(with => "--table +$table");
    
$Wikia::DW::Common::DEBUG = 1 if $debug;

my $strp = new DateTime::Format::Strptime(pattern => "%F %T");

$begin = $strp->parse_datetime($begin) if $begin;
$end   = $strp->parse_datetime($end)   if $end;

# CamelCase the table name for looking up ETL class
my $classname = $table;
$classname =~ s/_/ /g;
$classname =~ s/\b([a-z])/\u$1/g;
$classname =~ s/ //g;

# Extract whether the table type is Fact, Rollup, or Dimension
my ($classtype) = $table =~ m/^([^_]+)/;
$classtype =~ s/^(.)/\u$1/;

#
my $job;

my $pkg;
foreach my $try ("Wikia::DW::ETL::$classname", "Wikia::DW::ETL::$classtype") {
    eval "use $try";
    if (my $err = $@) {
        my $path = $try.'.pm';
        $path =~ s!::!/!g;
        die "Couldn't load $try: $@\n" unless ($err =~ /Can't locate $path/);
    } else {
        $pkg = $try;
        last;
    }
}

$job = $pkg->new( table   => $table,
                  periods => $periods,
                  begin   => $begin,
                  end     => $end,
                  rebuild => $rebuild,
                  debug   => $debug );

$job->process();

