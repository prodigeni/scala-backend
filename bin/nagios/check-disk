#!/usr/bin/perl
#
# check-disk  -  Check disk usage
#

use strict;
use warnings;

use Nagios::Plugin;

our ($PROGNAME) = $0 =~ m!([^/]+)$!;
our $VERSION = '1.0';

my $np = Nagios::Plugin->new(
	usage     => "Usage: %s [ -w <warning_threshold> ] [ -c <critical_threshold> ]\n"
		   . '   [ -m <mount_point> ]',
	version   => $VERSION,
	plugin    => $PROGNAME,
	shortname => uc($PROGNAME),
	blurb     => 'Check free disk space against given threshholds',
	timeout   => 30,
);

$np->add_arg(
  spec => 'warning|w=s',
  help => "-w, --warning=THRESHOLD\n"
    . "   Warning threshold (as a percentage) for free disk space.",
  required => 0,
);

$np->add_arg(
  spec => 'critical|c=s',
  help => "-c, --critical=THRESHOLD\n"
    . "   Critical threshold (as a percentage) for free disk space.",
  required => 0,
);

$np->add_arg(
  spec => 'mount|m=s',
  help => "-m, --mount=MOUNT\n"
    . "   Mount point to check",
  default => '/',
  required => 0,
);

$np->getopts;

# Make sure the mount point exists
my $mount = $np->opts->mount;
if (! -e $mount) {
  $np->nagios_exit('UNKNOWN', "Mount point '$mount' does not exist");
}

my $verbose = $np->opts->verbose;

# Set defaults and normalize
my $warning = $np->opts->warning || '80%';
my $critical = $np->opts->critical || '95%';
$warning =~ s/%//;
$critical =~ s/%//;

my ($used) = `df -h $mount | tail -1 | awk '{print \$5}'` =~ m!^(\d+)!;

$np->nagios_exit('CRITICAL', "Unable to determine used disk space") unless defined $used;

$np->set_thresholds(
	warning  => $warning,
	critical => $critical,
);

$np->add_perfdata(
  label     => "used",
  value     => $used,
  uom       => '%',
  threshold => $np->threshold,
);

$np->nagios_exit($np->check_threshold($used), "$used\% used");
