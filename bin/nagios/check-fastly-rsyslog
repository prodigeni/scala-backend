#!/usr/bin/perl
#
# check-disk  -  Check for rsyslog stream from fastly
#

use strict;
use warnings;

use Nagios::Plugin;
use Time::Local;

use constant LOG      => '/var/log/track/view/view.log';
use constant MIN_RATE => 400;

our ($PROGNAME) = $0 =~ m!([^/]+)$!;
our $VERSION = '1.1';
our %MON = (jan => 0, feb => 1, mar => 2, apr => 3, may => 4, jun => 5,
			jul => 6, aug => 7, sep => 8, oct => 9, nov => 10, dec => 11);
our $MAX_DELAY = 5;

my $np = Nagios::Plugin->new(
	usage     => "Usage: %s",
	version   => $VERSION,
	plugin    => $PROGNAME,
	shortname => uc($PROGNAME),
	blurb     => 'Make sure rsyslog stream is active',
	timeout   => 30,
);

# Determine if rsyslog is running
my $status = `sudo service rsyslog status`;

$np->nagios_exit(CRITICAL, "Rsyslog is stopped") unless $status =~ /running/;

# The last line should be within seconds of the current time
my $cmd = "tail -1 ".LOG();
my $last_line = `$cmd`;
my $now = time;

# Exit if we couldn't find a line
$np->nagios_exit('UKNOWN', "Could not determine if log is receiving traffic")
	unless $last_line;

# Grab the date from the log line
my ($M, $D, $h, $m, $s) = $last_line =~ /(\w{3}) (\d{2}) (\d{2}):(\d{2}):(\d{2})/;

# Exit if we can't find the date in the line
$np->nagios_exit('UKNOWN', "Log line did not contain a recognizable date/time")
	unless $M && $D && $h && $m && $s;

# The log line doesn't have year so just assume this year
my @ts = localtime($now);
my $secs = timelocal($s, $m, $h, $D, $MON{lc($M)}, $ts[5]);
my $delta = $now - $secs;

# Exit if the last line is too old
$np->nagios_exit('CRITICAL', "No traffic in over $MAX_DELAY seconds") if $delta > $MAX_DELAY;

# Check the data input rate for this file.  It gets rotated every 15m on the 15,
# so wait until there is at least a minute of data before checking
sleep(61-$ts[0]) if ($ts[1] % 15) == 0;

my $log = LOG();
my $lines = `/usr/wikia/backend/bin/rsyslog/lines-per-second $log`||0;
chomp($lines);

if ($lines < MIN_RATE()) {
	$np->nagios_exit('CRITICAL', "Data rate of $lines lines/s below minimum of ".MIN_RATE()." lines/s"); 
}

$np->nagios_exit('OK', "Fastly rsyslog stream OK");
