#!/usr/bin/perl
#
# check-scribe-buffer
#
# Monitor the files scribe buffers when the primary store becomes unavailable
#

use strict;
use warnings;

use Nagios::Plugin;
use Time::Local;

use constant LOG => '/etc/sv/scribe/log/main/current';

our ($PROGNAME) = $0 =~ m!([^/]+)$!;
our $VERSION = '1.0';
our $MAX_DELAY = 5;

my $np = Nagios::Plugin->new(
	usage     => "Usage: %s",
	version   => $VERSION,
	plugin    => $PROGNAME,
	shortname => uc($PROGNAME),
	blurb     => 'Make sure scribe stream is active',
	timeout   => 30,
);

$np->add_arg(
  spec => 'dir|d=s',
  help => "-d, --dir=DIRECTORY\n"
    . "   The location of the spool files",
  default => '/var/spool/scribe',
  required => 0,
);

$np->add_arg(
  spec => 'file|f=s',
  help => "-f, --file=FILE\n"
    . "   The base file name to monitor",
  required => 1,
);

$np->add_arg(
  spec => 'warning|w=s',
  help => "-w, --warning=THRESHOLD\n"
    . "   Warning threshold, in number of files.",
  required => 1,
);

$np->add_arg(
  spec => 'critical|c=s',
  help => "-c, --critical=THRESHOLD\n"
    . "   Critical threshold, in number of files.",
  required => 1,
);

$np->getopts;

my $base_path = $np->opts->dir.'/'.$np->opts->file;
my $num = `find $base_path* -type f | wc -l`;

# Just grab the number bit and ignore anything else
$num =~ s/^(\d+).*/$1/s;

$np->nagios_exit(CRITICAL, "Path: $base_path*; Number of buffer files: $num; Critical level: ".$np->opts->critical)
	if $num > $np->opts->critical;

$np->nagios_exit(WARNING, "Path: $base_path*; Number of buffer files: $num; Warning level: ".$np->opts->warning)
	if $num > $np->opts->warning;

$np->nagios_exit('OK', "Found $num buffer files");
